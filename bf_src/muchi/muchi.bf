set character code
>
+++++ +++++ +++++ +[
    >+++++ +++++ +++++ >+++++ +++++ ++++ >+++++ +++++ ++ >+++++ +++++ +
    >+++++ +++++ >+++++ +++++ >+++++ +++++ >+++++ ++++ >+++++ +++ >++ >++
    [<]>-
]
>- >++ >-- >>+++++ ++ >+ >----- -- >>>++++++++ >+

set boundary
>-

set command
>+ >++++ >+ >+++ >+ >++>++
>++++ >+++++ >+ >+ >+++ >++
>++++++++ >+ >+ >+++++++ >+++++ >+ >+ >++>++
>++++++ >++++++ >++++ >+ >+ >+++ >+ >++>++
>++++ >+++++++++ >+ >+ >+++ >++ >++
>+ >+ >++++ >+ >+ >+++ >+

# debug: dump [<]>[!>]<

decode command
+[-
    >+ e
    <-[ v
        -[
            -[
                -[
                    -[
                        -[
                            -[
                                -[  9 or more
                                    [-]>-  clear v; e
                                    <<[<]>  move leftmost
                                    >.>>>>>>>.<<<<-.+  dots
                                    [>] v
                                ]> e
                                [-  8
                                    <<[<]>
                                    >>>>> >>>>+.-  close paren
                                    [>]> e
                                ]< v
                            ]> e
                            [-  7
                                <<[<]>
                                >>>>> >>>>.  open paren
                                [>]> e
                            ]< v
                        ]> e
                        [-  6
                            <<[<]>
                            .>>.>>>>>+.-<<<<<<<  mu
                            .>>-.+>-.+<<<  ltu
                            .>>.>>>>>>+.-<<<<<<<<  chi
                            .>>-.+>>+.-<<<<  li
                            <+++++ +++++ ++[-
                                >.>>-.+>++.--<<<<  i
                            ]>
                            .>>-.+>-.+<<<  ltu
                            <++++[-
                                >>.>>>>>.<.<<<<<<  heart
                            ]>
                            [>]> e
                        ]< v
                    ]> e
                    [-  5
                        <<[<]>
                        >>>>> >>>>>.  exclaimation
                        [>]> e
                    ]< v
                ]> e
                [-
                    <<[<]>
                    >+.->>>>>>>.<---.+++  close brace
                    [>]> e
                ]< v
            ]> e
            [-
                <<[<]>
                >+.->>>>>>>.<----.++++  open brace
                [>]> e
            ]< v
        ]> e
        [-  2
            +++++ +++++.[-]  LF
        ]< v
    ]> e
    [-
        <<[<]>
        .>>.>>>>>+.-<<<<<<<  mu
        .>>.>>>>>>+.-<<<<<<<<  chi
        .>>-.+>-.+<<  ltu
        .>>>>>.<.  heart
        [>]> e
  ]< v
  <+  check next command
]
# debug: dump -[<]>[!>]<+
